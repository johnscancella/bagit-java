plugins {
  id 'java'
  
  //these have to be in the main project for now see - https://discuss.gradle.org/t/how-do-i-include-buildscript-block-from-external-gradle-script/7016/2
  id "com.github.kt3k.coveralls" version "2.8.1" 
  id "de.aaschmid.cpd" version "1.0"
  id "org.owasp.dependencycheck" version "1.4.5.1"
  id "com.dorongold.task-tree" version "1.3"
  id "org.ajoberstar.grgit" version "1.7.0"
  id "com.github.spotbugs" version "1.2"
}
apply from: 'eclipse.gradle'
apply from: 'maven-central.gradle'
apply from: 'code-quality.gradle'
apply from: 'message-bundle.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

if(project.version == "unspecified"){
  String now = new Date().format( 'MMM-dd-yyyy_HH-mm-ss' )
  project.version = "5.0.0-${now}-SNAPSHOT"
}

repositories {
  jcenter()
}

dependencies {
  compile 'org.slf4j:slf4j-api:1.7.25', 
  'com.fasterxml.jackson.core:jackson-core:2.9.0.pr4', 
  'com.fasterxml.jackson.core:jackson-databind:2.9.0.pr4'
    
  testCompile 'junit:junit:4.12', 
    'org.springframework.boot:spring-boot-starter-logging:1.5.4.RELEASE',
    'org.bouncycastle:bcprov-jdk15on:1.57',
    'org.kamranzafar:jtar:2.3'
}

test { //show test output
  testLogging.showStandardStreams = true
}

import org.ajoberstar.grgit.*
task cloneConformanceSuite(){
  group "Verification"
  description "Download the bagit-conformance-suite if it doesn't exist."
  File location = file("${project.projectDir}/bagit-conformance-suite")
  outputs.dir(location)
  
  onlyIf { !location.exists() }
  doLast{
      Grgit.clone(dir: location, 
                  uri: 'https://github.com/libraryofcongress/bagit-conformance-suite.git')
    }
}
